plugins {
    id "org.springframework.boot" version "2.2.6.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "java"
    id "jacoco"
    id 'pmd'
    id "io.freefair.lombok" version "5.0.0-rc6"
}

group = "com.cloudberry"
version = "0.0.1"
sourceCompatibility = "14"

bootJar {
    archiveFileName = "${archiveBaseName.get()}-${archiveVersion.get()}.${archiveExtension.get()}"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def springBootVersion = "2.2.6.RELEASE"
def springKafkaVersion = "2.4.4.RELEASE"
def apacheKafkaVersion = "2.4.1"
def influxVersion = "1.11.0"
def apacheCommonsLangVersion = "3.10"
def apacheCommonsMathVersion = "3.6.1"
def apacheCommonsCsvVersion = "1.8"

dependencies {
    implementation "com.influxdb:influxdb-client:$influxVersion"
    implementation "com.influxdb:influxdb-client-java:$influxVersion"
    implementation "com.influxdb:influxdb-client-core:$influxVersion"
    implementation "com.influxdb:influxdb-client-flux:$influxVersion"
    implementation "com.influxdb:influxdb-spring:$influxVersion"
    implementation "com.influxdb:flux-dsl:$influxVersion"
    implementation "org.influxdb:influxdb-java:2.20"

    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-logging:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb-reactive:$springBootVersion"
    implementation "org.springframework.kafka:spring-kafka:$springKafkaVersion"
    implementation "org.apache.kafka:kafka-streams:$apacheKafkaVersion"
    implementation "org.apache.kafka:kafka-clients:$apacheKafkaVersion"
    implementation "io.vavr:vavr:0.10.2"
    implementation "org.apache.commons:commons-lang3:$apacheCommonsLangVersion"
    implementation "org.apache.commons:commons-math3:$apacheCommonsMathVersion"
    implementation "org.apache.commons:commons-csv:$apacheCommonsCsvVersion"
    implementation "com.google.guava:guava:29.0-jre"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
    testImplementation("org.springframework.boot:spring-boot-starter-test:$springBootVersion") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0"
    testImplementation "io.projectreactor:reactor-test:3.3.4.RELEASE"
    testImplementation "org.springframework.kafka:spring-kafka-test:$springKafkaVersion"
    testImplementation "org.apache.kafka:kafka-clients:$apacheKafkaVersion"
    testImplementation "org.apache.kafka:kafka_2.12:$apacheKafkaVersion"
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation group: 'com.github.fakemongo', name: 'fongo', version: '2.1.1'
}

pmd {
    ignoreFailures = true
    consoleOutput = true
    ruleSetFiles = files("/pmd-ruleset.xml")
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = "INSTRUCTION"
                value = "COVEREDRATIO"
                minimum = 0.0
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}


test {
    useJUnitPlatform()
}

check.dependsOn(jacocoTestCoverageVerification, jacocoTestReport)
